plugins {
    id 'java'
    id 'jacoco'
}

ext.version_options = [
        SPRING_BOOT_VERSION: "2.2.5.RELEASE",
        SPRING_VERSION     : "5.2.4.RELEASE",
        JETTY_VERSION      : "9.4.16.v20140903",
        JUNIT_VERSION      : "9.4.16.v20140903",
        MOCKITO_VERSION    : "2.2.22"
]
ext.pack_version = (hasProperty("pack_version") ? property("pack_version") : version)

group = 'org.structutil'
version = '3.0.2.beta-SNAPSHOT'

ext.struct_jvm_projects = [
        ':struct-all',
        ':struct-core',
        ':struct-spring',
        ':struct-spring-boot-starter'
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.setEncoding("UTF-8")
    compileTestJava.options.setEncoding("UTF-8")

    group = "${rootProject.group}"
    version = "${rootProject.version}"

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/spring" }
        maven { url "https://repo1.maven.org/maven2/" }
        maven { url "https://repo.maven.apache.org/maven2/" }
        maven { url "http://repo.typesafe.com/typesafe/repo" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        //  log4j 2.x
        implementation('org.apache.logging.log4j:log4j-slf4j-impl:2.12.1')
        implementation('org.apache.logging.log4j:log4j-api:2.12.1')
        implementation('org.apache.logging.log4j:log4j-core:2.12.1')
        implementation('org.slf4j:slf4j-api:1.7.30')
        implementation('org.slf4j:jcl-over-slf4j:1.7.30')
        //  junit 5.x and mockito
        testImplementation("org.mockito:mockito-core:2.25.0")
        testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    }

    test {
        ignoreFailures = true
    }
}


/// <editor-fold desc="codecov">

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/report.xml")
        html.enabled true
        html.destination file("${buildDir}/reports/html")
        csv.enabled false
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/bootstrap/**'
                    ])
        })
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
/// </editor-fold>


//  <editor-fold desc="   Maven Publish  Local ">
//  https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:install

Closure init_publish_module = {

    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc {
            options.encoding = 'UTF-8'
            failOnError = false
        }
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            maven(MavenPublication) {
//                artifactId = "struct-util"
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            mavenLocal()
            maven {
                name "wooduan_nexus"
                url "http://192.168.0.2:8081/nexus/content/repositories/snapshots"
                credentials {
                    username = 'admin'
                    password = 'admin123'
                }
            }
        }
    }
}
ext.struct_jvm_projects.each {
    project(it, init_publish_module.curry())
}
/// </editor-fold>

